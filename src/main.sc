theme: /

    state: Start
        q!: $regex</start>
        a: Добро пожаловать в наш кинотеатр! Как я могу помочь вам?

    state: GetALoyaltyCard
        q!: * {[как/где/что/хочу] (оформить/получить/приобрести/сделать/завести) (~карта/~карточка) [лояльн*/клуб*]} *
        a: Выберите тип карты лояльности, которую вы хотите завести:
        buttons:
            "Пластиковая карта" -> /GetAPlasticCard
            "Виртуальная карта" -> /GetAVirtualCard
        
    state: GetAPlasticCard
        a: Вы можете бесплатно получить пластиковую карту лояльности при покупке билетов на кассе кинотеатра.
        
    state: GetAVirtualCard
        a: Зарегестрируйтесь на сайте кинотеатра и перейдите в раздел "получить карту".

    state: WhatIsLoyaltyCard
        q!: * {(что такое/как работает) (~карта/~карточка) [лояльн*/клуб*/виртуальн*]} *
        go!: /PrivilegesLoyaltyCard

    state: PrivilegesLoyaltyCard
        q!: * {(зачем/для чего/какие привелегии/какие бонусы) [нужна/есть] [у] * (~карта/~карточка) [лояльн*/клуб*]} *
        a: С применением карты лояльности после каждой покупки билетов и барной продукции будут начисляться бонусы. Баллы начисляются в количестве 10% от суммы покупки. Накопленные баллы можно тратить на покупку билетов и барной продукции киноеатра. 1 балл = 1 рубль.
        inlineButtons:
            {text:"Подробнее", url:"https://www.youtube.com/watch?v=dQw4w9WgXcQ"}
        
        state: HowToGetIt
            q: * {[как/где/что/хочу] * (оформить/получить/приобрести/сделать/завести)} *
            go!: /HowToGetACard

    state: HowToPayWithBonuses
        q!: * {[как] (*платить/купить/*бронировать) * (~бонусы/~баллы/~карта/~карточка) [лояльн*/клуб*] [билет*/покупк*]} *
        a: Для оплаты покупки баллами на кассе кинотеатра предъявите вашу виртуальную или пластиковую карту лояльности. Если вы хотите забронировать билет онлайн, нажмите на кнопку "оплатить картой лояльности" после выбора сеанса и мест.
    
    state: HowToSaveBonuses
        q!: * {[как] (*копить/*начисляют*/получать) (бонусы/[бонусные] баллы)} *
        a: С применением карты лояльности после каждой покупки билетов и барной продукции будут начисляться бонусы. Баллы начисляются в количестве 10% от суммы покупки. Начисление происходит через сутки после начала сеанса. 1 балл = 1 рубль.

    state: BonusesWereNotCredited
        q!: * {[почему] [мне] [на карту] (не (зачислили*/пришли/прислали*)) (бонусы/[бонусные] баллы)} *
        a: Пожалуйста, выберите способ покупки, которым вы воспользовались:
        buttons:
            "Online" -> /BonusesWereNotCreditedOnline
            "В кинотеатре" -> /BonusesWereNotCreditedOffline

    state: BonusesWereNotCreditedOnline
        a: Бонусы не начисляются в следующих случаях: при покупке онлайн-продукции бара; при покупке на сайте, если вы не авторизированы. Также обращаем ваше внимание на то, что баллы могут быть начислены в течении 24 часов после покупки. Если причина в ином, нажмите на кнопку ниже для связи с оператором:
        buttons:
            "Связаться с оператором" -> /Switch

    state: BonusesWereNotCreditedOffline
        a: Если вы совершали покупку на кассе кинотеатра с применением карты лояльности, и прошло более 24 часов с момента покупки, нажмите на кнопку ниже для связи с оператором:
        buttons:
            "Связаться с оператором" -> /Switch

    state: Switch
        TransferToOperator:
            titleOfCloseButton = Переключить обратно на бота
            messageBeforeTransfer = Подождите немного. Соединяю вас со специалистом.
            ignoreOffline = false
            messageForWaitingOperator = Вам ответит первый освободившийся оператор.
            noOperatorsOnlineState = /Switch/Error
            dialogCompletedState = /AnythingElse
            sendMessageHistoryAmount = 3
            sendMessagesToOperator = true

        state: Error
            a: К сожалению, все операторы сейчас заняты. Мы обязательно свяжемся с вами позже.
            go!: /AnythingElse

    state: AnythingElse
        a: Я могу вам еще чем-нибудь помочь?
        
        state: No
            q: * {*(не*/не надо/не нужно)*} *
            a: До свидания! Мы всегда ждем вас в нашем кинотеатре :)

    state: LoyaltyCardLoss
        q!:  * {(*терял*/*теря) (~карта/~карточка) [лояльн*/клуб*]} *
        a: Если ваша карта потерялась, нажмите на кнопку ниже для связи с оператором. Назовите оператору номер утерянной карты, чтобы мы могли её заблокировать и выпустить новую.
        buttons:
            "Связаться с оператором" -> /Switch
    
    state: NoMatch
        event!: noMatch
        a: Извините, я вас не понял. Попробуйте переформулировать вопрос.

        state: InnerNoMatch
            event: noMatch
            a: К сожалению, я опять не смог вас понять. Если вы хотите связаться с оператором, нажмите на кнопку ниже:
            buttons:
                "Связаться с оператором" -> /Switch